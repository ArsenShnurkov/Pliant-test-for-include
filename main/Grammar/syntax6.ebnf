file := ws , [ include_directive , { ows , include_directive } , ws ] ;

include_directive := include_keyword , iws , include_path ;
include_keyword := ("i" | "I") , ("n" | "N") , ("c" | "C") , ("l" | "L") , ("u" | "U") , ("d" | "D") , ("e" | "E") ;
include_path := [ slash | backslash ] , part_of_path , { ( slash | backslash ) , part_of_path } , [ slash | backslash ] ;

other_directive := other_keyword, { other_directive_fragment | glue } ;

other_keyword := ( other_keyword_symbol , { other_keyword_symbol } ) - include_keyword ;
other_keyword_symbol := ( other_directive_fragment | backslash ) - ( iws | newline );
other_directive_fragment  := ac - ( cr | lf | backslash ) 
  | backslash , ac - ( cr | lf ) ;
glue := backslash , newline ;

part_of_path := start_of_path , { body_of_path } , end_of_path ;
start_of_path := body_of_path ;
body_of_path := ac - ( slash | backslash | iws | newline ) ;
end_of_path := (* empty rule *) ; 

ws := [ ows ] ; (* white space *)
ows := ows_element , { ows_element } ; (* obligatory white space *)
ows_element := iws_element | newline | other_directive ;
iws := iws_element , { iws_element } ; 
iws_element := space_or_tab | glue | comment ;

space_or_tab := ( space | tab ) , { space | tab } ;
newline := cr | lf | cr , lf ;

comment := comment_start, comment_body , comment_end ;
comment_start := { space_or_tab } , "#" ;
comment_body := { ac - ( lf | cr ) } ;
comment_end := (* empty rule *) ;

letter := ? Terminals.Letter ? ;
letter_or_digit := ? Terminals.LetterOrDigit ? ;
ac := ? Terminals.AnyChar ? ;

tab := "\x09" ;
lf := "\x0A" ;
cr := "\x0D" ;
space := "\x20" ;
asterisk := "\x2A" ;
dot := "\x2E" ;
slash := "\x2F" ;
backslash := "\x5C" ;
